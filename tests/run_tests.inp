set verbose off
clear

include assertion.gfn

include "./src/BMST.inp" --force


bundles Parameters = null
Parameters = Parameters + _(command = "logit", expected = FALSE)
Parameters = Parameters + _(command = "foo", expected = TRUE)
function void test_model_isnot_logit (const bundles P)
    print "Start testing model_isnot_logit()."

    loop foreach i P
        # Given + When
        actual = model_isnot_logit(P[i].command)

         # Then
         assert(actual == P[i].expected)
    endloop
end function
test_model_isnot_logit(Parameters)


bundles Parameters = null
Parameters = Parameters + _(command = "probit", expected = FALSE)
Parameters = Parameters + _(command = "logit", expected = FALSE)
Parameters = Parameters + _(command = "foo", expected = TRUE)
function void test_model_isnot_logit_probit (const bundles P)
    print "Start testing model_isnot_logit_or_probit()."

    loop foreach i P
        # Given + When
        actual = model_isnot_logit_or_probit(P[i].command)

         # Then
         assert(actual == P[i].expected)
    endloop
end function
test_model_isnot_logit_probit(Parameters)


open greene19_1.gdt -q -p
series GPA_w_missings = GPA
GPA_w_missings[19] = NA
series GRADE_w_missings = GRADE
GRADE_w_missings[19] = NA
bundles Parameters = null

Parameters = Parameters + \
            _(which= "binary_homoskedasticity", endog = deflist(GRADE),\
            xlist = deflist(const, GPA_w_missings), expected_error = TRUE)
Parameters = Parameters + \
            _(which= "binary_homoskedasticity", endog = deflist(GRADE),\
            xlist = deflist(const, GPA), expected_error = FALSE)
Parameters = Parameters + \
            _(which= "binary_homoskedasticity", endog = deflist(GRADE_w_missings),\
            xlist = deflist(const, GPA), expected_error = TRUE)

Parameters = Parameters + \
            _(which= "binary_reset", endog = deflist(GRADE),\
            xlist = deflist(const, GPA_w_missings), expected_error = TRUE)
Parameters = Parameters + \
            _(which= "binary_reset", endog = deflist(GRADE),\
            xlist = deflist(const, GPA), expected_error = FALSE)
Parameters = Parameters + \
            _(which= "binary_reset", endog = deflist(GRADE_w_missings),\
            xlist = deflist(const, GPA), expected_error = TRUE)

Parameters = Parameters + \
            _(which= "binary_burr", endog = deflist(GRADE),\
            xlist = deflist(const, GPA_w_missings), expected_error = TRUE)
Parameters = Parameters + \
            _(which= "binary_burr", endog = deflist(GRADE),\
            xlist = deflist(const, GPA), expected_error = FALSE)
Parameters = Parameters + \
            _(which= "binary_burr", endog = deflist(GRADE_w_missings),\
            xlist = deflist(const, GPA), expected_error = TRUE)
function void test_tests_w_missings (const bundles P)
    print "Start testing how tests handle missing values."

    loop foreach i P
    eval i
        # Given
        series y = P[i].endog[1]
        list xlist = P[i].xlist
        logit y xlist -q

        # When
        string funcname = P[i].which
        bundle Output = @funcname($model)

         # Then
         assert((Output.error > 0) == P[i].expected_error)
    endloop
end function
test_tests_w_missings(Parameters)



open greene19_1.gdt -q -p
function void test_binary_reset_probit (const list L)
    print "Start testing binary_reset() for Probit"

    probit GRADE 0 GPA TUCE PSI -q
    bundle test = binary_reset($model)

    assert_almost_equal_num(test.test, -0.277238, 1e-6)
    assert_almost_equal_num(test.pvalue, 0.781597, 1e-6)
end function
test_binary_reset_probit(dataset)


open greene19_1.gdt -q -p
function void test_binary_homoskedasticity_lo (const list L)
    print "Start testing binary_homoskedasticity for Logit()"

    logit GRADE 0 GPA TUCE PSI -q
    bundle test = binary_homoskedasticity($model)

    assert_almost_equal_num(test.test, 3.05054, 1e-5)
    assert_almost_equal_num(test.pvalue, 0.383898, 1e-6)
end function
test_binary_homoskedasticity_lo(dataset)


open greene19_1.gdt -q -p
function void test_binary_homoskedasticity_pr (const list L)
    print "Start testing binary_homoskedasticity for Logit()"

    probit GRADE 0 GPA TUCE PSI -q
    bundle test = binary_homoskedasticity($model)

    assert_almost_equal_num(test.test, 2.72025, 1e-5)
    assert_almost_equal_num(test.pvalue, 0.436797, 1e-6)
end function
test_binary_homoskedasticity_pr(dataset)



open greene19_1.gdt -q -p
function void test_binary_burr (const list L)
    print "Start testing binary_burr()"

    logit GRADE 0 GPA TUCE PSI -q
    bundle test = binary_burr($model)

    assert_almost_equal_num(test.test, 0.025283, 1e-6)
    assert_almost_equal_num(test.pvalue, 0.873664, 1e-6)
end function
test_binary_burr(dataset)



printf "\nInfo: All tests passed.\n"
